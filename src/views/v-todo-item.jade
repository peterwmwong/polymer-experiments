polymer-element( name='v-todo-item', extends='li', \attributes='item' )
  link( rel='import', href='v-todo-input.html' )

  template
    link( rel='stylesheet', href='v-todo-item.css' )

    #btnBack( on-click="onEndEditingTodo" )
      | &lt;

    #wrap( on-click='onStartEditingTodo' )

      #panelContainer

        .basicPanel
          .deleteTodo.btn-cancel( on-click='onDeleteTodo' )

          .editTodoTextWrap
            input#editTodoText.editTodoText( is='v-todo-input', value='{{ item.text }}', on-focused='onStartEditingTodo' )
            a.editTodoTextReadOnly
              | {{ item.text }}

        //- TODO: extract into new element
        .detailedPanelContainer
          #detailedPanel
            .header NOTES
            .section
              textarea.notes( value='{{ item.notes }}', placeholder='Leave yourself a note...', on-focus='onNotesFocus' )


  :coffeescript
    ANIMATION_DURATION_MS = 400
    Polymer 'v-todo-item',
      item: undefined
      editing: false
      ready: ->
        @editing = false

      preventOverscroll: (e)->
        # TODO: polymer/CustomElement/something is wrapping elements in a way
        #       that scrollTop can't be set!
        el = @$.wrap.impl
        startTopScroll = el.scrollTop

        if startTopScroll <= 0
          el.scrollTop = 1

        if startTopScroll + el.offsetHeight >= el.scrollHeight
          el.scrollTop = el.scrollHeight - el.offsetHeight - 1
        return

      onEndEditingTodo: ->
        # Animation start
        @editing = false
        @$.panelContainer.setAttribute 'style', "-webkit-transform: translate3d(0,#{@offsetTop - 48 - document.body.scrollTop}px,0);"
        @$.detailedPanel.setAttribute 'style', '-webkit-transform: translate3d(0,-100%,0);'
        @$.wrap.classList.add 'bgout'
        @$.wrap.style.backgroundColor = 'rgba(0,0,0,0)'

        setTimeout (=>
          @$.panelContainer.setAttribute 'style', ''
          @$.detailedPanel.setAttribute 'style', ''
          @$.btnBack.classList.remove 'editing'
          @$.wrap.classList.remove 'editing'
          @$.wrap.classList.remove 'bgout'
          @$.wrap.style.backgroundColor = ''
        ), ANIMATION_DURATION_MS

      onDeleteTodo: ->
        @fire 'deletetodo', @item

      onNotesFocus: ->
        window.scrollTo 0, 0

      onStartEditingTodo: (e)->
        savedScrollY = window.scrollY
        window.scrollTo 0, savedScrollY
        @asyncMethod =>
          window.scrollTo 0, savedScrollY

        unless @editing
          @editing = true
          # Animation before start
          @$.panelContainer.setAttribute 'style', "-webkit-transform: translate3d(0,#{@offsetTop - 48 - document.body.scrollTop}px,0);"

          requestAnimationFrame =>
            # Animation start
            @$.panelContainer.setAttribute 'style', '-webkit-transform: translate3d(0,0,0);'
            @$.btnBack.classList.add 'editing'
            @$.wrap.classList.add 'editing'

            @$.wrap.addEventListener 'touchstart', ((e)=>@preventOverscroll(e)), true



